apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply from: rootProject.file('dependencies.gradle')

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId versions.groupId
            artifactId versions.artifactId
            version versions.versionName

            pom.withXml {
                def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }

            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/$versions.artifactId-release.aar")
        }
    }
}


Properties properties = new Properties()
properties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintrayUser")
    key = properties.getProperty("bintrayApiKey")
    publications = ['MyPublication']
    pkg {
        repo = "maven"
        name = "$versions.groupId:$versions.artifactId"
        desc = versions.desc
        licenses = ['Apache-2.0']
        vcsUrl = versions.vsc
        version {
            name = versions.versionName
            vcsTag = versions.versionName
            released = new Date()
        }
    }
}
